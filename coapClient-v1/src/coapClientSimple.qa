System coapSimpleClient

Event value_event : value(Distance, Angle)

Dispatch value : value(Distance, Angle)
Dispatch stopMessage : stopMessage

Context ctxCoapSimpleClient ip [ host="localhost" port=8055 ]

EventHandler handlevalue for value_event {
	forwardEvent client_simple_actor -m value
};

QActor client_simple_actor context ctxCoapSimpleClient{
		Plan init normal
	[ 
		println("coapClientSimple: start.");
		javaRun it.unibo.radar.coap.client.coapRadarClientSimple.initClient()
	]
	switchTo putValue
	
	Plan putValue
	[
		println("coapClientSimple: Emitted PUT: value(45,90).");
		javaRun it.unibo.radar.coap.client.coapRadarClientSimple.putResourceValue("45", "90")
	]
	switchTo getValue
	
	Plan getValue resumeLastPlan
	[
		println("coapClientSimple: Emitted GET.");
		javaRun it.unibo.radar.coap.client.coapRadarClientSimple.getResourceValue()
	]
	transition stopAfter 2000
		whenMsg value -> printValue
	
	Plan printValue
	[
		println("coapClientSimple: Response GET received.");
		println("coapClientSimple: Distance = ");
		onMsg value : value(Distance, Angle) -> println(Distance);
		println("coapClientSimple: Angle = ");
		onMsg value : value(Distance, Angle) -> println(Angle)	
	]
	switchTo stopping	
	
	Plan stopping
	[
		println("coapClientSimple: stop.")
	]
	
}